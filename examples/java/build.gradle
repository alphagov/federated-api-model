plugins {
  id 'com.diffplug.spotless' version '6.0.4'
  id "org.sonarqube" version "3.3"
  id 'uk.gov.api.common-conventions'
}

repositories {
  mavenCentral()
}

allprojects {
  apply plugin: 'com.diffplug.spotless'

  spotless {
    java {
      target 'src/**/*.java'
      googleJavaFormat('1.13.0')
      trimTrailingWhitespace()
      endWithNewline()
    }
    groovyGradle {
      target '**/*.gradle'
      greclipse()
      indentWithSpaces(2)
      trimTrailingWhitespace()
      endWithNewline()
    }
    format 'xml', {
      target 'src/**/*.xml'
      eclipseWtp('xml', '4.21.0')
    }
  }

  tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
  }
}

sonarqube {
  properties {
    property "sonar.projectKey", "co-cddo_federated-api-model_examples-java"
    property "sonar.organization", "co-cddo"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}
