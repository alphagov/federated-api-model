import org.springframework.cloud.contract.verifier.config.TestFramework

buildscript {
  dependencies {
    classpath 'org.jsonschema2pojo:jsonschema2pojo-gradle-plugin:1.1.1'
  }
}

plugins {
  id 'uk.gov.api.spring-boot-conventions'
  id "org.springframework.cloud.contract" version '3.1.0'
}

apply plugin: 'jsonschema2pojo'

dependencies {
  implementation 'org.apache.logging.log4j:log4j-layout-template-json'
  implementation 'org.springframework.boot:spring-boot-starter-log4j2'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'io.rest-assured:json-schema-validator:4.4.0'
  testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
}

task copySchemas(type: Copy) {
  from '../../../schemas/v1alpha'
  include '*.json'
  into file("${project.buildDir}/resources/test/schemas/v1alpha")
}
processTestResources.finalizedBy copySchemas

jsonSchema2Pojo {
  source = files(fileTree(dir: '../../../schemas/v1alpha', include: ['*.json']))
  targetDirectory = file("${project.buildDir}/generated-sources/js2p")
  targetPackage = "uk.gov.api.models.metadata"
}

contracts {
  testFramework = TestFramework.JUNIT5
  baseClassForTests = 'uk.gov.api.springboot.contract.BaseTestClass'

  contractRepository {
//    repositoryUrl = "git://https://github.com/spring-cloud-samples/spring-cloud-contract-nodejs-contracts-git.git"
    repositoryUrl = "git://git@github.com:co-cddo/federated-api-model-contracts.git"
  }
  contractsProperties =
[
      'git.branch' : 'main'
    ]

  contractsMode = "REMOTE"
}

contractTest {
  useJUnitPlatform()
}

test.finalizedBy contractTest
